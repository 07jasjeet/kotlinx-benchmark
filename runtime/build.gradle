import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_plugin_version"
    }
}

plugins {
    id 'maven-publish'
    id 'org.jetbrains.kotlin.multiplatform' version "1.3.20"
}

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    js {
        compilations.all {
            kotlinOptions {
                sourceMap = true
                moduleKind = "umd"
            }
        }
    }

    // For ARM, preset should be changed to presets.iosArm32 or presets.iosArm64
    // For Linux, preset should be changed to e.g. presets.linuxX64
    // For MacOS, preset should be changed to e.g. presets.macosX64
    macosX64()

    sourceSets {
        commonMain {
            kotlin.srcDirs = ["$it.name/src"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            kotlin.srcDirs = ["$it.name/src"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            kotlin.srcDirs = ["$it.name/src"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                compileOnly "org.openjdk.jmh:jmh-core:$jmhVersion"
            }
        }
        jvmTest {
            kotlin.srcDirs = ["$it.name/src"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            kotlin.srcDirs = ["$it.name/src"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
        jsTest {
            kotlin.srcDirs = ["$it.name/src"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
        
        macosX64Main {
            kotlin.srcDirs = ["nativeMain/src"]
        }
        macosX64Test {
            kotlin.srcDirs = ["nativeTest/src"]
        }
    }

    sourceSets.all {
        resources.srcDirs = ["$it.name/resources"]
        languageSettings.progressiveMode = true
    }
}


publishing {
    def user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    def key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    repositories {
        maven {
            name = 'bintray'
            def bintrayOrg = project.bintrayOrg
            def bintrayRepository = project.bintrayRepository
            def bintrayPackage = project.bintrayPackage
            url = "https://api.bintray.com/maven/$bintrayOrg/$bintrayRepository/$bintrayPackage/;publish=0"
            credentials {
                username =  user
                password = key
            }
        }
    }
}