buildscript {
    repositories {
        mavenCentral()
        jcenter()
        // maven { url "../../kotlin-libraries-infra/build/maven" }
        maven { url "https://dl.bintray.com/orangy/maven" }
    }
    dependencies {
        classpath "kotlinx.team:kotlinx.team.infra:0.1.0-dev-26"
    }

}

plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version "1.3.30"
}

apply plugin: 'kotlinx.team.infra'

infra {
    publishing {
        include(":")

        bintray {
            organization = 'orangy'
            repository = 'maven'
            library = 'gradle-benchmarks'

            username = findProperty('bintrayUser')
            password = findProperty('bintrayApiKey')
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
//    maven { url "../../kotlin-libraries-infra/build/maven" }
    maven { url "https://dl.bintray.com/orangy/maven" }
}

gradlePlugin {
    plugins {
        benchmarkPlugin {
            id = "org.jetbrains.gradle.benchmarks.plugin"
            implementationClass = "org.jetbrains.gradle.benchmarks.BenchmarksPlugin"
        }
    }
}

sourceSets {
    main {
        kotlin.srcDirs = ['main/src']
        java.srcDirs = ['main/src']
        resources.srcDirs = ['main/resources']
    }
    test {
        kotlin.srcDirs = ['test/src']
        java.srcDirs = ['test/src']
        resources.srcDirs = ['test/resources']
    }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk7', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version

    compile 'com.squareup:kotlinpoet:1.0.0'
    compile "org.openjdk.jmh:jmh-core:$jmhVersion"

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-native-library-reader', version: kotlin_version, {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-compiler'
    }

    compileOnly "kotlinx.team:kotlinx.team.infra:0.1.0-dev-26"

    compileOnly group: 'org.jetbrains.kotlin.multiplatform', name: 'org.jetbrains.kotlin.multiplatform.gradle.plugin', version: kotlin_version
    compileOnly "org.openjdk.jmh:jmh-generator-bytecode:$jmhVersion" // used in worker

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.12'
    
/*
    // This is needed for test to get it on classpath and run
    runtime "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    runtime "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
*/
}
